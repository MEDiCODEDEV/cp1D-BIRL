HORA DO SHOW
    // Gerador aleatorio
    MONSTRO iseed = 10324849;
    MONSTRO AMAG=843314861;
    MONSTRO BMAG=453816693;
    MONSTRO im=1073741824;
    MONSTRO a1=65539;
    TRAPEZIO r_1=1.0/(4.0*im);
    
    MONSTRO L = 1000;
    TRAPEZIO lambda = 3.14;
    TRAPEZIO p = 1.0/(1.0+1.0*lambda);
    
    TRAPEZIO t = 0.0;
    TRAPEZIO tmax = 1000.0;
    TRAPEZIO tpos = 1.0;
    TRAPEZIO dt;
    MONSTRO pos;
    MONSTRO ver;
    MONSTRO viz;
    TRAPEZIO z;
    MONSTRO sig[L];
    MONSTRO ocp[L];
    MONSTRO voc = L; // TODO MUNDO TA DOENTE
    
    MONSTRO i;
    MAIS QUERO MAIS (i = 0; i < L; i++)
        sig[i] = 1;
        ocp[i] = i;
    BIRL
    
    OH O HOME AI PO (MONSTRO ipl(MONSTRO i))
            MONSTRO a;
            ELE QUE A GENTE QUER? (i == L-1)
                a = 0;
            NAO VAI DAR NAO
                a = i + 1;
            BIRL
          BORA CUMPADE a;
    BIRL
    OH O HOME AI PO (MONSTRO imi(MONSTRO i))
            MONSTRO a;
            ELE QUE A GENTE QUER? (i == 0)
                a = L-1;
            NAO VAI DAR NAO
                a = i - 1;
            BIRL
          BORA CUMPADE a;
    BIRL
    OH O HOME AI PO (TRAPEZIO rnd())
        iseed=(AMAG*iseed)+BMAG;
        BORA CUMPADE r_1*iseed+0.5;
    BIRL
    OH O HOME AI PO (MONSTRO irnd(MONSTRO a, MONSTRO b))
        TRAPEZIO x;
        MONSTRO num;
        x = AJUDA O MALUCO TA DOENTE rnd();
        num = x*(b+1-a)+a;
        ELE QUE A GENTE QUER? (num > b)
            num = b;
        BIRL
        BORA CUMPADE num;
    BIRL
    
    CE QUER VER ESSA PORRA? ("\n");
    CE QUER VER ESSA PORRA? ("# TAMANHO DA JAULA: %d\n", L);
    CE QUER VER ESSA PORRA? ("# TAXA DE INFECCAO DESSA PORRA: %f\n", lambda);
    CE QUER VER ESSA PORRA? ("# TEMPO E FRACAO DE MALUCOS DOENTES:\n");
    
    NEGATIVA BAMBAM (t < tmax)
        pos = AJUDA O MALUCO TA DOENTE irnd(0,voc-1);
        ver = ocp[pos];
        z = AJUDA O MALUCO TA DOENTE rnd();
        
        ELE QUE A GENTE QUER? (z < p)
            sig[ver] = 0;
            ocp[pos] = ocp[voc-1];
            voc = voc - 1;
        NAO VAI DAR NAO
            pos = irnd(0,1);
            ELE QUE A GENTE QUER? (pos == 0)
                viz = ipl(ver);
            NAO VAI DAR NAO
                viz = imi(ver);
            BIRL
            
            ELE QUE A GENTE QUER? (sig[viz] == 0)
                sig[viz] = 1;
                voc = voc + 1;
                ocp[voc-1] = viz;
            BIRL    
        BIRL
        ELE QUE A GENTE QUER? (voc == 0)
            break;
        BIRL
        t = t + 1.0*p/voc;
        
        ELE QUE A GENTE QUER? (t > tpos)
            CE QUER VER ESSA PORRA? ("%.10f\t%.10f\n", t, ((float)voc/L));
            tpos = tpos + 1;
        BIRL
    BIRL
    
    BORA CUMPADE 0;
BIRL
